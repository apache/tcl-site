#! ../bin/websh3

web::logfilter add *.-debug
web::logdest add *.-debug file "../logs/bugsubmit.log"

source ../lib/websh.com.gen

proc page {nr code} {

    startHtmlPage
    startHtmlBody
    putHeader $nr

    uplevel $code

    finishPage $nr
}

proc form {page code} {

    page 3 {

	web::putx {
	    <table border="0" cellpadding="0" cellspacing="0" width="770">
	    {headRow 4 "Bug submission form"}
	    <tr valign="top">
	    {navbar_supp 3}
	    <td width="455">

	    {
		web::put "<form method=\"POST\" action=\"[web::cmdurl $page]\">"
		uplevel $code
		web::put "</form>"
	    }

	    </td>
	    {newsbar_supp}
	    </tr>
	    </table>
	}
    }
}

proc putErrorMessage {msg} {
    # emit an error message in red.
    web::put "<p><font color=\"\#ff0000\">[web::htmlify $msg]</font></p>"
}

# confirmation page
proc confirm {} {

    page 3 {
	web::putx {
	    <table border="0" cellpadding="0" cellspacing="0" width="770">
	    {headRow 4 "Bug submission form"}
	    <tr valign="top">
	    {navbar_supp 1}
	    <td width="455">

	    {
		web::put "<h3>thank you</h3>\n"

		web::put "Your submission:<br>\n"
		web::put "<b>Short Description:</b><br>\n"
		web::put "[web::htmlify -- [web::formvar sdsc]]<br>\n"
		#
		web::put "<b>webshell version:</b>\n"
		web::put "[web::htmlify -- [web::formvar webshver]]<br>\n"
		#
		web::put "<b>Severity:</b><br>\n"
		web::put "[web::htmlify -- [web::formvar severity]]<br>\n"
		#
		web::put "<b>Description:</b><br>\n"
		web::put "[web::htmlify -- [web::formvar ldsc]]<br>\n"
		#
		if {[string length [web::formvar email]] > 1} {
		    web::put "<b>e-mail:</b><br>\n"
		    web::put "[web::htmlify -- [web::formvar email]]<br>\n"
		}

		web::put "has been forwarded to our websh experts."
	    }

	    </td>
	    {newsbar_supp}
	    </tr>
	    </table>
	}
    }
}

proc emailify {list} {
    foreach item $list {
	append res [xmlify $item]
    }
    return $res
}

proc xmlify {varName} {
    set res "<$varName>"
    if {[string length [web::formvar $varName]] > 1} {
	append res "[web::formvar $varName]\n"
    }
    append res "</$varName>\n"

    return $res
}

# process submission
web::command doSubmit {

    if { [string length [web::formvar sdsc]] < 1 } {

        submit "Please enter a short description."
    } elseif { [string length [web::formvar webshver]] < 1 } {

        submit "Please enter the webshell version."
    } elseif { [string length [web::formvar email]] > 1 } {

	if { ![check_email [web::formvar email] ] } {

	    submit "Please enter a valid e-mail address (if any)."
	}
    }

    # send by e-mail
    set fh [open "| /usr/lib/sendmail support@websh.com" w]
    if { [string length [web::formvar email]] > 1 } {
	puts $fh "From: [web::formvar email]"
    }
    puts $fh "Subject: \[ws3-bug\] [web::formvar sdsc]"
    puts $fh ""

    set tmp [emailify [list sdsc webshver severity ldsc email]]

    puts $fh $tmp
    close $fh

    web::log info $tmp

    confirm
}

# check_email
proc check_email {address} {
    return [regexp "\[^ \.\]+@\[^ \t\]+\\\.\[^ \t\]+$" $address]
}

# input -- format the "submit" button
proc input {type name size value} {
    set res "<input type=\"$type\" name=\"$name\" size=\"$size\" "
    append res "value=\"$value\">\n"
    return $res
}

# textarea
proc textarea {rows cols name value} {
    set res "<textarea rows=\"$rows\" cols=\"$cols\" name=\"$name\">"
    append res $value
    append res "</textarea>\n"
    return $res
}

# select -- ooutput HTML combobox
proc select {name list selected} {
    set res "<select name=\"$name\">\n"
    foreach option $list {
	if {$option == $selected} {
	    append res "<option selected>$option\n"
	} else {
	    append res "<option>$option\n"
	}
    }
    append res "\n</select>\n"
    return $res
}

# show bug submission form
proc submit {{errorMessage ""}} {
    form doSubmit {
        if {[string length $errorMessage]} {
            putErrorMessage $errorMessage
        }
        web::put "<b>Short Description:</b><br>\n"
	web::put "[input text sdsc 45 [web::formvar sdsc]]<br>\n"
	#
        web::put "<b>webshell version:</b> (use web::copyright -version)\n"
	web::put "[input text webshver 45 [web::formvar webshver]]<br>\n"
	#
        web::put "<b>Severity:</b><br>\n"
	web::put "[select severity [list suggestion glitch bug fatal] [web::formvar severity]]<br>\n"
	#
        web::put "<b>Description:</b><br>\n"
	web::put "[textarea 10 45 ldsc [web::formvar ldsc]]<br>\n"
	#
        web::put "If you would like to be notified when this bug is "
        web::put "fixed, you can add your e-mail:<br>"
        web::put "<b>e-mail:</b> (optional)<br>\n"
	web::put "[input text email 45 [web::formvar email]]<br>\n"
	#
        web::put "<input type=\"submit\" value=\"submit\">"
        web::put "<input type=\"reset\" value=\"reset\">"
    }
}


web::command default {

    submit
}

web::dispatch
