== webshell tutorial ==

This tutorial demonstrates the first steps with
webshell. We assume that you are familiar with Tcl syntax.

-- webshell concepts --

In the following, we will use the webshell concepts
for command dispatching, output management, formvar management, and
logging. The respective webshell commands we will use are

==* web::command
==* web::cmdurl
==* web::put
==* web::putx
==* web::formvar
==* web::param
==* web::log
==* web::logfilter
==* web::logdest

-- task --

Create a script that shows an HTML form and handles its submission

Challenge: the form must at least supply three fields: a name, an
address and an e-mail address (the address field should be a
<pre><textarea></pre> field) the reply after the submission must
reflect the input the script must provide two commands: the default,
and the submit command.

-- solution --

<pre>
#!/websh3

# default command
web::command default {

    # note: it's usually more efficient to take web::eval instead of web::puts
    # to output composed strings and header and footer procs usually generate
    # their own output. -> see step 2 for a better implementation
    web::puts "[header Form]
<form method=post action=\"[web::cmdurl submit]\">

<dl>
 <dt><b>Name:</b>
 <dd><input type=text name=name value=\"\" size=30><p>
 <dt><b>Address:</b>
 <dd><textarea name=addr rows=4 cols=30 wrap=auto></textarea><p>
 <dt><b>E-Mail:</b>
 <dd><input type=text name=email value=\"\" size=30><p>
</dl>

<input type=reset name=cancel value=\"Reset\">
<input type=submit name=ok value=\"Send\">

</form>

[footer]"

}


# submit command
web::command submit {

    # take care of linebreaks in address
    regsub -all "\r\n" [web::htmlify [web::formvar addr]] "<br>" addr

    # note: it's usually more efficient to take web::eval instead of web::puts
    # to output composed strings and header and footer procs usually generate
    # their own output. -> see step 2 for a better implementation
    web::puts "[header Confirmation]
<h3>Thank you ...</h3>

<dl>
 <dt><b>Name:</b>
 <dd>[web::htmlify [web::formvar name]]<p>
 <dt><b>Address:</b>
 <dd>$addr<p>
 <dt><b>E-Mail:</b>
 <dd>[web::htmlify [web::formvar email]]<p>
</dl>

[footer]"

}


proc header {{title ""}} {

    set completeTitle "Websh Tutorial - Step 1 - $title"

    return "<html>
<head>
<title>$completeTitle</title>
</head>

<body bgcolor=\"\#ffffff\" text=\"\#000000\">

<h2>$completeTitle</h2>

"

}

proc footer {} {

    return "</body>
</html>
"

}

</pre>
